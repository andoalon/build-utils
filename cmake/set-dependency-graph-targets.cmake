cmake_minimum_required(VERSION 3.17)
include_guard()

if (NOT CMAKE_SOURCE_DIR STREQUAL PROJECT_SOURCE_DIR)
    message(WARNING "Disabled dependency graph targets since this is not the top-level project")
    return() # Disabled if not top-level project
endif()

find_program(conan_executable "conan" #[[optional]] DOC "conan package manager, in order to generate graphs from conan data")

set(conan_generate_graphviz)
set(conan_generate_graphviz_images)
if (EXISTS "${CMAKE_SOURCE_DIR}/conanfile.txt" OR EXISTS "${CMAKE_SOURCE_DIR}/conanfile.py")
    # Set graphviz options to avoid having noise from configuration-specific conan packages,
    # unless the project already has its own graphviz options
    if (NOT EXISTS "${CMAKE_SOURCE_DIR}/CMakeGraphVizOptions.cmake")
        file(WRITE "${CMAKE_BINARY_DIR}/CMakeGraphVizOptions.cmake"
"# GENERATED BY ${CMAKE_CURRENT_LIST_FILE}\n\
#\n\
# Ignore the configuration-specific versions of\n\
# packages that are generated by conan's multi config generator\n\
set(GRAPHVIZ_IGNORE_TARGETS\n\
    \"(DEBUG|RELEASE|RELWITHDEBINFO|MINSIZEREL)$\" # From conan\n\
)\n"
        )
    endif()

    if (conan_executable)
        set(conan_generate_graphviz COMMAND conan info "${CMAKE_SOURCE_DIR}" "--graph=${CMAKE_BINARY_DIR}/graphs/conan-dependencies.dot")
        set(conan_generate_graphviz_images COMMAND conan info "${CMAKE_SOURCE_DIR}" "--graph=${CMAKE_BINARY_DIR}/graphs/images/conan-dependencies.html")
    endif()
endif()

add_custom_target("graphviz"
    COMMAND "${CMAKE_COMMAND}" -E rm -rf "${CMAKE_BINARY_DIR}/graphs"
    COMMAND "${CMAKE_COMMAND}" -E make_directory "${CMAKE_BINARY_DIR}/graphs"
    COMMAND "${CMAKE_COMMAND}" "--graphviz=graphs/${PROJECT_NAME}.dot" "${CMAKE_SOURCE_DIR}"
    ${conan_generate_graphviz}
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}"
    VERBATIM
)

add_custom_target("graphviz-images"
    COMMAND "${CMAKE_COMMAND}" -E rm -rf "${CMAKE_BINARY_DIR}/graphs/images"
    ${conan_generate_graphviz_images}
    COMMAND "${CMAKE_COMMAND}" "-Dgraph_files_dir=${CMAKE_BINARY_DIR}/graphs" -P "${CMAKE_CURRENT_LIST_DIR}/convert-dependency-graphs-to-images.cmake"
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}/graphs"
    VERBATIM
)

add_dependencies("graphviz-images" "graphviz")

set_target_properties("graphviz-images" "graphviz"
    PROPERTIES
        FOLDER "dependency-graphs"
)

unset(conan_generate_graphviz)
unset(conan_generate_graphviz_images)
